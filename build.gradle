
plugins {
    id 'idea'
    id 'jacoco'
    id 'java'
    id 'java-library'
    id 'maven-publish'

    id 'com.diffplug.spotless' version '6.16.0'
    id 'com.github.davidmc24.gradle.plugin.avro' version '1.6.0'
    id 'com.github.spotbugs' version '5.0.13'
    id 'com.jfrog.artifactory' version '4.31.5'
    id 'org.sonarqube' version '4.0.0.2929'
}

configurations.all {
  // Bump required by CVE-2022-42920. This update will be released in spotbugs 4.7.4.
  resolutionStrategy.force 'org.apache.bcel:bcel:6.6.1'
}

group = gradle.projectGroup

project.ext {
    isCiServer = System.getenv().containsKey("CI")
}

// If true, search local repository (~/.m2/repository/) first for dependencies.
def useMavenLocal = false
repositories {
    if (useMavenLocal) {
        mavenLocal() // must be listed first to take effect
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release-local/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

// for scans
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

def gradleIncDir= "$rootDir/gradle"
apply from: "$gradleIncDir/dependencies.gradle"
apply from: "$gradleIncDir/dependency-locking.gradle"
apply from: "$gradleIncDir/jacoco.gradle"
apply from: "$gradleIncDir/javadoc.gradle"
apply from: "$gradleIncDir/publishing.gradle"
apply from: "$gradleIncDir/sonarqube.gradle"
apply from: "$gradleIncDir/spotbugs.gradle"
apply from: "$gradleIncDir/spotless.gradle"
apply from: "$gradleIncDir/testing.gradle"
