import com.github.davidmc24.gradle.plugin.avro.GenerateAvroJavaTask

plugins {
    id 'idea'
    id 'jacoco'
    id 'java'
    id 'java-library'
    id 'maven-publish'

    id 'com.diffplug.spotless' version '6.16.0'
    id 'com.github.davidmc24.gradle.plugin.avro' version '1.6.0'
    id 'com.github.spotbugs' version '5.0.13'
    id 'com.jfrog.artifactory' version '4.31.5'
}

// If true, search local repository (~/.m2/repository/) first for dependencies.
def useMavenLocal = false
repositories {
    gradlePluginPortal()
    if (useMavenLocal) {
        mavenLocal() // must be listed first to take effect
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release-local/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

// for scans
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

def gradleIncDir= "$rootDir/gradle"
apply(from: "$gradleIncDir/dependencies.gradle")
apply from: "$gradleIncDir/dependency-locking.gradle"
apply from: "$gradleIncDir/jacoco.gradle"
apply from: "$gradleIncDir/javadoc.gradle"
apply from: "$gradleIncDir/publishing.gradle"
apply from: "$gradleIncDir/spotbugs.gradle"
apply from: "$gradleIncDir/spotless.gradle"
apply from: "$gradleIncDir/testing.gradle"

generateAvroJava {
    source("src/main/avro/Metadata.avsc")
    source("src/main/avro/Environment.avsc")
    source("src/main/avro/LandingZone.avsc")
    outputDir = file("${buildDir}/avro")
}
